ext {
    junitPlatformVersion = '1.5.2'
    junitJupiterVersion = '5.5.2'
}
repositories {
    mavenCentral()
}

version = '0.1.1-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

tasks['publish'].dependsOn(build)

// In this section you declare the dependencies for your production and test code
dependencies {

    implementation("org.junit.platform:junit-platform-launcher:${junitPlatformVersion}")

    testImplementation(
            "org.junit.jupiter:junit-jupiter:${junitJupiterVersion}",
            "org.junit-pioneer:junit-pioneer:0.5.1")
    testCompile(
            "org.assertj:assertj-core:3.14.0",
            "org.mockito:mockito-core:3.2.4",
            "org.jetbrains:annotations:16.0.2"
    )

}

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.compilerArgs += '-parameters'
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
    systemProperties = [
            //"hue.client"        : "3O2Ce9zBt9ed7Ht9kdvAlYVbLr7ycmVtTOc6FICU",
            //"hue.client"        : "Y0YtVSXtBGLv1K5-jTIj07u51fiedXKRGjzeiv-W",
            "hue.client": "8GFTbkftIvDT0SWSyV4LliHJICB870JVRr5TkTk1"

            //optional:
            //"hue.ip": "192.168.178.49",
            //"hue.timeout"       : 2500,
            //"hue.listener.lamps": ["1", "2", "3"]
    ]

}

publishing {
    repositories {
        maven {
            def ossrhUsername = project.hasProperty('ossrhUsername') ? project.ossrhUsername : ''
            def ossrhPassword = project.hasProperty('ossrhPassword') ? project.ossrhPassword : ''

            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }

            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = isSnapshotRelease() ? snapshotsRepoUrl : releasesRepoUrl
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId = 'de.klosebrothers.hue'
            artifactId = 'hue4junit'
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'hue4junit'
                groupId = 'de.klosebrothers.hue'

                description = 'JUnit5 TestExecutionListener for Philips hue lights'
                url = 'https://github.com/mklose/hue4junit'


                licenses {
                    license {
                        name = 'Eclipse Public License - v 2.0'
                        url = 'http://www.eclipse.org/legal/epl-v20.html'
                    }
                }
                developers {
                    developer {
                        id = 'mklose'
                        name = 'Martin Klose'
                        email = 'mklose@klosebrothers.de'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:mklose/hue4junit.git'
                    developerConnection = 'scm:git:git@github.com:mklose/hue4junit.git'
                    url = 'https://github.com/mklose/hue4junit'
                }
            }
        }
    }
}

boolean isSnapshotRelease() {
    version.endsWith('SNAPSHOT')
}

signing {
    if (!isSnapshotRelease()) {
        sign publishing.publications.mavenJava
    }
}

wrapper {
    gradleVersion = '5.6.2'
}
